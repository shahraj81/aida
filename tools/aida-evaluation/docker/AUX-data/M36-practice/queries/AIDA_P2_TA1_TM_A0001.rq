PREFIX ldcOnt: <https://tac.nist.gov/tracks/SM-KBP/2019/ontologies/LDCOntology#>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX aida:  <https://tac.nist.gov/tracks/SM-KBP/2019/ontologies/InterchangeOntology#>
PREFIX cfn:   <https://verdi.nextcentury.com/custom-function/>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>

# The query to pick up start and end time ranges of events and relations

SELECT DISTINCT
       ?cluster
       (substr(str(?_sa_month), 3) as ?sa_month) (substr(str(?_sa_day), 4) as ?sa_day) (str(?_sa_year) as ?sa_year)
	     (substr(str(?_sb_month), 3) as ?sb_month) (substr(str(?_sb_day), 4) as ?sb_day) (str(?_sb_year) as ?sb_year)
	     (substr(str(?_ea_month), 3) as ?ea_month) (substr(str(?_ea_day), 4) as ?ea_day) (str(?_ea_year) as ?ea_year)
       (substr(str(?_eb_month), 3) as ?eb_month) (substr(str(?_eb_day), 4) as ?eb_day) (str(?_eb_year) as ?eb_year)

WHERE {

    ?cluster              a                             aida:SameAsCluster .
    ?cluster              aida:prototype                ?prototype .
    ?prototype            aida:ldcTime                  ?time .

    ?time                 a                             aida:LDCTime .
    ?time                 aida:start                    ?start_after_time .
    ?time                 aida:start                    ?start_before_time .
    ?time                 aida:end                      ?end_after_time .
    ?time                 aida:end                      ?end_before_time .

    ?start_after_time     a                             aida:LDCTimeComponent .
    ?start_after_time     aida:timeType                 "AFTER" .
    OPTIONAL {?start_after_time     aida:day                      ?_sa_day .}
    OPTIONAL {?start_after_time     aida:month                    ?_sa_month .}
    OPTIONAL {?start_after_time     aida:year                     ?_sa_year .}

    ?start_before_time    a                             aida:LDCTimeComponent .
    ?start_before_time    aida:timeType                 "BEFORE" .
    OPTIONAL {?start_before_time    aida:day                      ?_sb_day .}
    OPTIONAL {?start_before_time    aida:month                    ?_sb_month .}
    OPTIONAL {?start_before_time    aida:year                     ?_sb_year .}

    ?end_after_time       a                             aida:LDCTimeComponent .
    ?end_after_time       aida:timeType                 "AFTER" .
    OPTIONAL {?end_after_time       aida:day                      ?_ea_day .}
    OPTIONAL {?end_after_time       aida:month                    ?_ea_month .}
    OPTIONAL {?end_after_time       aida:year                     ?_ea_year .}

    ?end_before_time      a                             aida:LDCTimeComponent .
    ?end_before_time      aida:timeType                 "BEFORE" .
    OPTIONAL {?end_before_time      aida:day                      ?_eb_day .}
    OPTIONAL {?end_before_time      aida:month                    ?_eb_month .}
    OPTIONAL {?end_before_time      aida:year                     ?_eb_year .}

}

ORDER BY ?cluster ?sa_month ?sa_day ?sa_year ?sb_month ?sb_day ?sb_year ?se_month ?ea_day ?ea_year ?eb_month ?eb_day ?eb_year 
