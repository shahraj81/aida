PREFIX ldcOnt: <https://raw.githubusercontent.com/NextCenturyCorporation/AIDA-Interchange-Format/master/java/src/main/resources/com/ncc/aif/ontologies/LDCOntologyM36#>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX aida:  <https://raw.githubusercontent.com/NextCenturyCorporation/AIDA-Interchange-Format/master/java/src/main/resources/com/ncc/aif/ontologies/InterchangeOntology#>
PREFIX cfn:   <https://verdi.nextcentury.com/custom-function/>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>

# QueryID: AIDA_P2_TA2_P0002

SELECT DISTINCT
       ?docid                         # sourceDocument
       ?query_link_target             # link target as part of the query e.g. "REFKB:30005137"
       ?link_target                   # link target in the KB matching ?query_link_target
       ?cluster                       # the ?cluster linked to ?link_target
       ?mention_span                  # informativeJustification span taken from the prototype of the ?cluster
       (str(?_j_cv) as ?j_cv)         # confidenceValue of informativeJustification
       (str(?_link_cv) as ?link_cv)   # confidenceValue of asserting that prototype of the ?cluster is the same as reference KB node ?link_target

WHERE {
    BIND ("REFKB:30005137" AS ?query_link_target)

    ?cluster              a                             aida:SameAsCluster .
    ?cluster              aida:prototype                ?prototype .
    ?prototype            aida:informativeJustification ?justification .
    ?prototype            aida:link                     ?ref_kb_link .
    ?ref_kb_link          a                             aida:LinkAssertion .
    ?ref_kb_link          aida:linkTarget               ?link_target .
    ?ref_kb_link          aida:confidence               ?link_confidence .
    ?link_confidence      aida:confidenceValue          ?_link_cv .

    FILTER(cfn:memberOf(str(?link_target), str(?query_link_target))) .

    # Find mention spans for ?justification
    ?justification        aida:source                   ?doceid .
    ?justification        aida:sourceDocument           ?docid .
    ?justification        aida:confidence               ?j_confidence .
    ?j_confidence         aida:confidenceValue          ?_j_cv .

    OPTIONAL {
           ?justification a                             aida:TextJustification .
           ?justification aida:startOffset              ?so .
           ?justification aida:endOffsetInclusive       ?eo
    }
    OPTIONAL {
           ?justification a                             aida:ImageJustification .
           ?justification aida:boundingBox              ?bb1  .
           ?bb1           aida:boundingBoxUpperLeftX    ?ulx1 .
           ?bb1           aida:boundingBoxUpperLeftY    ?uly1 .
           ?bb1           aida:boundingBoxLowerRightX   ?lrx1 .
           ?bb1           aida:boundingBoxLowerRightY   ?lry1
    }
    OPTIONAL {
           ?justification a                             aida:KeyFrameVideoJustification .
           ?justification aida:keyFrame                 ?kfid .
           ?justification aida:boundingBox              ?bb2  .
           ?bb2           aida:boundingBoxUpperLeftX    ?ulx2 .
           ?bb2           aida:boundingBoxUpperLeftY    ?uly2 .
           ?bb2           aida:boundingBoxLowerRightX   ?lrx2 .
           ?bb2           aida:boundingBoxLowerRightY   ?lry2
    }
    OPTIONAL {
           ?justification a                             aida:ShotVideoJustification .
           ?justification aida:shot                     ?sid
    }
    OPTIONAL {
           ?justification a                             aida:AudioJustification .
           ?justification aida:startTimestamp           ?st1 .
           ?justification aida:endTimestamp             ?et1
    }
    OPTIONAL {
           ?justification a                             aida:VideoJustification .
           ?justification aida:startTimestamp           ?st2 .
           ?justification aida:endTimestamp             ?et2
    }

    BIND( IF( BOUND(?sid), ?sid, "__NULL__") AS ?_sid) .
    BIND( IF( BOUND(?kfid), ?kfid, "__NULL__") AS ?_kfid) .
    BIND( IF( BOUND(?so), ?so, "__NULL__") AS ?_so) .
    BIND( IF( BOUND(?eo), ?eo, "__NULL__") AS ?_eo) .
    BIND( COALESCE(?st1, ?st2, "__NULL__") AS ?_st) .
    BIND( COALESCE(?et1, ?et2, "__NULL__") AS ?_et) .
    BIND( COALESCE(?ulx1, ?ulx2, "__NULL__") AS ?_ulx) .
    BIND( COALESCE(?uly1, ?uly2, "__NULL__") AS ?_uly) .
    BIND( COALESCE(?lrx1, ?lrx2, "__NULL__") AS ?_lrx) .
    BIND( COALESCE(?lry1, ?lry2, "__NULL__") AS ?_lry) .

    BIND( cfn:getSpan(str(?docid), str(?doceid), str(?_sid), str(?_kfid), str(?_so), str(?_eo), str(?_ulx), str(?_uly), str(?_lrx), str(?_lry), str(?_st), str(?_et) ) AS ?mention_span ) .
}
ORDER BY ?docid ?cluster ?mention_span
